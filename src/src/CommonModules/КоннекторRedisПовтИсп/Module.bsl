#Область ПрограммныйИнтерфейс

// Клиент redis.
// 
// Возвращаемое значение:
//  РазрешеннаяВнешняяКомпонента
Функция КлиентRedis() Экспорт
	
	Если Не КоннекторRedisПовтИсп.Используется() Тогда
		ВызватьИсключение "Использование Redis отключено в функцональных опциях";
	КонецЕсли;
	
	Если Не ПодключитьВнешнююКомпоненту("ОбщийМакет.RedisClient", "Redis", ТипВнешнейКомпоненты.Native) Тогда
		ВызватьИсключение "Не удалось подключить компоненту";
	КонецЕсли;
	
 	Компонента = Новый("AddIn.Redis.RedisClient");
	Если Компонента = Неопределено Тогда
		ВызватьИсключение "Не удалось создать экземпляр компоненты";
	КонецЕсли;             
	
	Если КешRedisПовтИсп.СравнитьВерсии(Компонента.ВерсияКомпоненты, "1.0.0.1") < 0 Тогда
		ВызватьИсключение "Используется версия компоненты ниже поддерживаемой";
	КонецЕсли;
	Компонента.Connect(АдресСервераRedis());   
	
	Возврат Компонента;
	
КонецФункции

Функция АдресСервераRedis() Экспорт
	УстановитьПривилегированныйРежим(Истина);
	Возврат Константы.RedisURI.Получить();
КонецФункции

Функция Используется() Экспорт
	Возврат ПолучитьФункциональнуюОпцию("ИспользоватьRedis");
КонецФункции

#КонецОбласти